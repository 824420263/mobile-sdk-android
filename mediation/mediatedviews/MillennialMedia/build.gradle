// Project Properties
version = "6.0.0" // supported Millennial version

apply plugin: 'com.android.library'

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName version
        consumerProguardFiles 'proguard-project.txt'
    }

    sourceSets {
        main.manifest.srcFile 'AndroidManifest.xml'
        main.java.srcDir 'src'
        main.res.srcDir 'res'
        main.assets.srcDir 'assets'
        main.resources.srcDir 'src'
    }

    lintOptions {
        abortOnError false
    }

}

apply from: 'fat-aar.gradle'

ext {
    pom_google_play_services_version = "8.4.0"
}

dependencies {
    compile project(':sdk')
    // When build MillennialMedia from source
    // Instead of embed the mm-ad-sdk, compile it
//     compile project(':mm-ad-sdk')
    embedded project(':mm-ad-sdk')
    // MM crashes the app if the right google play services package is not present in the app
    // To reduce pain in the the MM building from source integration, adding the dependencies here
    compile("com.google.android.gms:play-services-base:$project.pom_google_play_services_version") {
        transitive = false
    }
    compile("com.google.android.gms:play-services-ads:$project.pom_google_play_services_version") {
        transitive = false
    }
}

/**
 * Maven distribution
 */
// create a gradle.properties file and assign the following properties for the script to use your credentials
//nexusUsername=
//nexusPassword=
//nexusRepositoryUrl=
//nexusRepositoryUrlReleases= //optional
//nexusRepositoryUrlSnapshots= // optional
// To release the sdk to maven, please run
// gradle clean assembleRelease publishMillennialPublicationToMavenRepository
// Can alternatively pass the parameters from command line using -PcustomProp=myProp

apply plugin: 'maven-publish'

ext {
    pom_name = "AppNexus Android SDK: Millennial Media Mediation Adapter"
    pom_description = "AppNexus's Android Advertising Libraries: Mediation adapter for Millennial Media"
    pom_url = "https://wiki.appnexus.com/display/sdk/Mobile+SDKs?src=search#MobileSDKs-Android"

    pom_groupId = "com.appnexus.opensdk.mediatedviews"
    pom_artifactId = "appnexus-millennialmedia-mediation"
    pom_packaging = "aar"

    pom_distribution_snapshotRepo_id = "ossrh"
    pom_distribution_snapshotRepo_url = "https://oss.sonatype.org/content/repositories/snapshots"
    pom_distribution_repo_id = "ossrh"
    pom_distribution_repo_url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
}

publishing {
    publications {
        millennial(MavenPublication) {
            groupId project.pom_groupId
            artifactId project.pom_artifactId
            version project(':sdk').version + "-" + project.version
            artifact("${project.buildDir}/outputs/aar/${project.name}-release.aar")

            pom.withXml {
                asNode().appendNode('description',
                        project.pom_description)
                asNode().appendNode('name',
                        project.pom_name)
                def scmNode = asNode().appendNode('scm')
                scmNode.appendNode('connection', project(':sdk').pom_scm_connection)
                scmNode.appendNode('developerConnection', project(':sdk').pom_scm_developerConnection)
                scmNode.appendNode('url', project(':sdk').pom_scm_url)
                def licencesNode = asNode().appendNode('licenses')
                def licenseNode = licencesNode.appendNode('license')
                licenseNode.appendNode('name', project(':sdk').pom_license_name)
                licenseNode.appendNode('url', project(':sdk').pom_license_url)
                def developersNode = asNode().appendNode('developers')
                def developerNode = developersNode.appendNode('developer')
                developerNode.appendNode('name', project(':sdk').pom_developer_name)
                developerNode.appendNode('email', project(':sdk').pom_developer_email)
                developerNode.appendNode('organization', project(':sdk').pom_developer_organization)
                developerNode.appendNode('organizationUrl', project(':sdk').pom_developer_organizationUrl)
                def dependenciesNode = asNode().appendNode('dependencies')
                def sdkDependencyNode = dependenciesNode.appendNode('dependency')
                sdkDependencyNode.appendNode('groupId', project(':sdk').pom_groupId)
                sdkDependencyNode.appendNode('artifactId', project(':sdk').pom_artifactId)
                sdkDependencyNode.appendNode('version', project(':sdk').version)
                sdkDependencyNode.appendNode('scope', 'compile')
                sdkDependencyNode.appendNode('type', project(':sdk').pom_packaging)
                // MM crashes the app if the right google play services package is not present in the app
                // To reduce pain in the the MM maven integration, adding the dependencies here
                def googleAdsDependencyNode = dependenciesNode.appendNode('dependency')
                googleAdsDependencyNode.appendNode('groupId', 'com.google.android.gms')
                googleAdsDependencyNode.appendNode('artifactId', 'play-services-ads')
                googleAdsDependencyNode.appendNode('version', project.pom_google_play_services_version)
                googleAdsDependencyNode.appendNode('type', 'aar')
                def googleBaseDependencyNode = dependenciesNode.appendNode('dependency')
                googleBaseDependencyNode.appendNode('groupId', 'com.google.android.gms')
                googleBaseDependencyNode.appendNode('artifactId', 'play-services-base')
                googleBaseDependencyNode.appendNode('version', project.pom_google_play_services_version)
                googleBaseDependencyNode.appendNode('type', 'aar')
            }
        }
    }
    repositories {
        maven {
            credentials {
                username project.hasProperty('nexusUsername') ? project.getProperties().get('nexusUsername') : "Replace your username here"
                password project.hasProperty('nexusPassword') ? project.getProperties().get('nexusPassword') : "Replace your password here"
            }
            url project.hasProperty('nexusRepositoryUrl') ? project.getProperties().get('nexusRepositoryUrl') : project.pom_distribution_repo_url
        }
    }
}


